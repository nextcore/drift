plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "{{.PackageName}}"
    compileSdk 34

    defaultConfig {
        applicationId "{{.PackageName}}"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86_64"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path file("src/main/cpp/CMakeLists.txt")
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["src/main/jniLibs"]
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation "com.google.android.gms:play-services-location:21.1.0"
    implementation platform("com.google.firebase:firebase-bom:32.7.0")
    implementation "com.google.firebase:firebase-messaging-ktx"
    implementation "androidx.security:security-crypto:1.1.0-alpha06"
    implementation "androidx.biometric:biometric:1.1.0"
    implementation "androidx.media3:media3-exoplayer:1.2.1"
    implementation "androidx.media3:media3-ui:1.2.1"
}

// Apply google-services plugin only if google-services.json exists
if (file("google-services.json").exists()) {
    apply plugin: "com.google.gms.google-services"
}

// Compile Drift Go code before building (for ejected projects)
// This task runs driftw (or driftw.bat on Windows) to locate the drift CLI and compile Go code
tasks.register("compileDrift", Exec) {
    // Two levels up: platform/android/ -> project root.
    // This assumes the ejected directory lives at <project>/platform/android/.
    workingDir = rootProject.projectDir.parentFile.parentFile
    def isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
    if (isWindows) {
        commandLine "cmd", "/c", "${rootProject.projectDir}\\driftw.bat", "compile", "android"
    } else {
        commandLine "${rootProject.projectDir}/driftw", "compile", "android"
    }
    onlyIf {
        def isWin = System.getProperty("os.name").toLowerCase().contains("windows")
        if (isWin) {
            file("${rootProject.projectDir}/driftw.bat").exists()
        } else {
            def driftw = file("${rootProject.projectDir}/driftw")
            driftw.exists() && driftw.canExecute()
        }
    }
}

tasks.named("preBuild") {
    dependsOn "compileDrift"
}
